// dialog initiliaziation abazigal lair
IF
    AreaCheck("AR6000")
    NumTimesTalkedTo(0)
    See([PC])
THEN
    RESPONSE #100
        StartDialogNoSet([PC])
END
// Generic Deathstalker last encounter failed attempt to escape
IF
    !Dead("Myself")
    HPPercentLT(Myself,40)
    Global("rqRetreatEncT7-0","LOCALS",0)
    Global("Enclave","GLOBAL",2)
    Global("rqTobRetreatImpossible","GLOBAL",0)
THEN
    RESPONSE #100
        CreateVisualEffectObject("SPDIMNDR",Myself)
        DisplayStringHead(Myself,@2) // Next time!
        SetGlobal("rqRetreatEncT7-0","LOCALS",1)
        DisplayStringHead(Myself,@3) // What?! The magic does not work! Someone betrayed us!
        SetGlobal("rqTobRetreatImpossible","GLOBAL",1)
END
// !Not last retreat
IF
    GlobalLT("Enclave","GLOBAL",2)
    Global("rqTobRetreatImpossible","GLOBAL",0)
    HPPercentLT(Myself,40)
    Global("rqRetreatEncT6orT7-0","LOCALS",0)
THEN
    RESPONSE #100
        ReallyForceSpell(Myself,DRYAD_TELEPORT)
        DisplayStringHead(Myself,74086) // Damn you!
        SetGlobal("rqRetreatEncT6orT7-0","LOCALS",1)
END
// Turn hostile script 6000 abazigal
IF
    Allegiance("rqt7clf1",ENEMY)  // Deathstalker Sixtarch
    !Allegiance(Myself,ENEMY)
    Global("rqAttackBegins1","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("rqAttackBegins1","LOCALS",1)
        Enemy()
END
// Turn hostile script 6100 Sendai
IF
    Allegiance("rqt6clf1",ENEMY)  // Deathstalker Itwantos
    !Allegiance(Myself,ENEMY)
    Global("rqAttackBegins2","LOCALS",0)
THEN
    RESPONSE #100
        SetGlobal("rqAttackBegins2","LOCALS",1)
        Enemy()
END
// Give Holy symbols
IF
    Global("rqGiveItemsControl","LOCALS",0)
    Global("rqTobGiveCharnItems","GLOBAL",1)
THEN
    RESPONSE #100
        SetGlobal("rqGiveItemsControl","LOCALS",1)
        GiveItem("rqhosy1",Player1)
END
// Escape Area2
IF
    Global("rqEscapeArea4","LOCALS",0)
    Global("rqTobEscapeArea","GLOBAL",1)
THEN
    RESPONSE #100
        SetGlobal("rqEscapeArea4","LOCALS",1)
        ClearAllActions()
        ChangeEnemyAlly(Myself,NEUTRAL)
        EscapeAreaNoSee()
END
// Ally
IF
    Global("rqChangeToALLY","LOCALS",0)
    Global("rqTobChangeToAlly","GLOBAL",1)
THEN
    RESPONSE #100
        SetGlobal("rqChangeToALLY","LOCALS",1)
        ClearAllActions()
        ChangeEnemyAlly(Myself,ALLY)
END
// Neutral stay in area Area2
IF
    Global("rqNeutral2","LOCALS",0)
    Global("rqTobNeutral","GLOBAL",1)
THEN
    RESPONSE #100
        SetGlobal("rqNeutral2","LOCALS",1)
        ClearAllActions()
        ChangeEnemyAlly(Myself,NEUTRAL)
END
